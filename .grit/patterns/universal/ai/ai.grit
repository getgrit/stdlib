language js

// Given a question and a list of choices,
function ai_ask($question, $choices) {
  $choice_text = join($choices, `, `),
  $messages = [
    {
      role: "system",
      content: `Answer all questions with a single word. Choices: $choice_text`
    },
    {
      role: "user",
      content: $question
    }
  ],
  $answer = llm_choice($messages, $choices),
  return $answer
}

// Match the given condition, optionally with examples and counter-examples.
pattern ai_is($condition, $examples, $counter_examples) {
  $item_to_check where {
    $messages = [
      {
        role: "system",
        content: `Classify whether the input code should match this condition: $condition. Answer with yes or no.`
      }
    ],
    $examplar_messages = [],
    if (!$examples <: undefined) {
      $examples <: some bubble($examplar_messages) $example where {
        $examplar_messages += {
          answer: `yes`,
          content: $example,
        }
      },
    },
    if (!$counter_examples <: undefined) {
      $counter_examples <: some bubble($examplar_messages) $counter_example where {
        $examplar_messages += {
          answer: `no`,
          content: $counter_example,
        }
      },
    },
    // Workaround for https://github.com/getgrit/rewriter/issues/8137
    $total_examples = length($examplar_messages),
    if (!$total_examples <: 0) {
      $shuffled_exemplars = shuffle($examplar_messages),
      $shuffled_exemplars <: some bubble($messages) $exemplar where {
        $messages += {
          role: "user",
          content: $exemplar.content
        },
        $messages += {
          role: "assistant",
          content: $exemplar.answer
        },
      },
    },
    $messages += {
      role: "user",
      content: $item_to_check
    },
    $answer = llm_choice($messages, choices=["yes", "no"]),
    $answer <: "yes"
  }
}
